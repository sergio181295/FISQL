options
{
  LOOKAHEAD=2;
}
PARSER_BEGIN(usql)
package usql;
import java.io.*;
 public class usql{
 }
PARSER_END(usql)

SKIP:
{
 <"\t"|" "|"\n"|"\r">
}

TOKEN [IGNORE_CASE]:{
    <INTEGER:"INTEGER">
    |<DOUBLE:"DOUBLE">
    |<DATE:"DATE">
    |<TEXT:"TEXT">
    |<BOOL:"BOOL">
    |<DATETIME:"DATETIME">
    |<VERDADERO:"VERDADERO">
    |<FALSO:"FALSO">
    |<CREAR:"CREAR">
    |<BASE_DATOS:"BASE_DATOS">
    |<TABLA:"TABLA">
    |<OBJETO:"OBJETO">
    |<PROCEDIMIENTO:"PROCEDIMIENTO">
    |<FUNCION:"FUNCION">
    |<USUARIO:"USUARIO">
    |<COLOCAR:"COLOCAR">
    |<PASSWORD:"PASSWORD">
    |<NULO:"NULO">
    |<NO_NULO:"NO_NULO">
    |<AUTOINCREMENTABLE:"AUTOINCREMENTABLE">
    |<LLAVE_PRIMARIA:"LLAVE_PRIMARIA">
    |<LLAVE_FORANEA:"LLAVE_FORANEA">
    |<REFERENCIA:"REFERENCIA">
    |<UNICO:"UNICO">
    |<USAR:"USAR">
    |<ALTERAR:"ALTERAR">
    |<CAMBIAR:"CAMBIAR">
    |<AGREGAR:"AGREGAR">
    |<QUITAR:"QUITAR">
    |<ELIMINAR:"ELIMINAR">
    |<BACKUP:"BACKUP">
    |<RESTAURAR:"RESTAURAR">
    |<USQLDUMP:"USQLDUMP">
    |<COMPLETO:"COMPLETO">
    |<RETORNO:"RETORNO">
    |<INSERTAR:"INSERTAR">
    |<EN:"EN">
    |<VALORES:"VALORES">
    |<DONDE:"DONDE">
    |<ACTUALIZAR:"ACTUALIZAR">
    |<BORRAR:"BORRAR">
    |<ORDENAR_POR:"ORDENAR_POR">
    |<SELECCIONAR:"SELECCIONAR">
    |<DE:"DE">
    |<ASC:"ASC">
    |<DESC:"DESC">
    |<OTORGAR:"OTORGAR">
    |<DENEGAR:"DENEGAR">
    |<PERMISOS:"PERMISOS">
    |<DECLARAR:"DECLARAR">
    |<SI:"SI">
    |<SINO:"SINO">
    |<SELECCIONA:"SELECCIONA">
    |<CASO:"CASO">
    |<DEFECTO:"DEFECTO">
    |<PARA:"PARA">
    |<MIENTRAS:"MIENTRAS">
    |<DETENER:"DETENER">
    |<IMPRIMIR:"IMPRIMIR">
    |<FECHA:"FECHA">
    |<FECHA_HORA:"FECHA_HORA">
    |<CONTAR:"CONTAR">
    
    |<MAS:"+">
    |<MENOS:"-">
    |<POR:"*">
    |<DIV:"/">
    |<POT:"^">
    |<MASMAS:"++">
    |<MENOSMENOS:"--">
    |<MENOR:"<">
    |<MENORIGUAL:"<=">
    |<MAYOR:">">
    |<MAYORIGUAL:">=">
    |<IGUALIGUAL:"==">
    |<NOIGUAL:"!=">
    |<OR:"||">
    |<AND:"&&">
    |<NOT:"!">
    |<IGUAL:"=">
    |<LLA1:"{">
    |<LLA2:"}">
    |<PAR1:"(">
    |<PAR2:")">
    |<PUNTO:".">
    |<COMA:",">
    |<PUNTOYCOMA:";">
    |<DOSPUNTOS:":">
    |<ASTERISCO:"**">

    |<ID: ("@")?["a"-"z"](["a"-"z"]|["0"-"9"]|"_")*>
    |<NUMERO:(["0"-"9"])+((["."]["0"-"9"])+)?>
    |<CADENA: "\""(~["\n","\r"])*"\"">
}



public boolean inicio():{}{
    sentencias(){
        System.out.println("Compilacion correcta!!");
        return true;
    }
}

void sentencias():{}{
    (sentencia())*

}

void sentencia():{}{
    ddl()
    |dml()
    |dcl()
    |ssl()
}

/** SENTENCIAS DDL */
    void ddl():{}{
        crear()
        |usar()
        |alterar()
        |eliminar()
        |backup()
        |restaurar()
        |retorno()
    }

    //DDL CREAR
        void crear():{}{
            <CREAR> crearCosa()
        }

        void crearCosa():{}{
            <BASE_DATOS> <ID> <PUNTOYCOMA>
            |<TABLA> <ID> <PAR1> atrTabla() <PAR2> <PUNTOYCOMA>
            |<OBJETO> <ID> <PAR1> atrTabla() <PAR2> <PUNTOYCOMA>
            |<PROCEDIMIENTO> <ID> <PAR1> atrTabla() <PAR2> <LLA1> sentencias()  <LLA2>
            |<FUNCION> <ID> <PAR1> atrTabla() <PAR2> tipo() <LLA1> sentencias() <LLA2>
            |<USUARIO> <ID> <COLOCAR> <PASSWORD> <IGUAL> valor() <PUNTOYCOMA>
        }

        void atrTabla():{}{
          tipo() <ID> (complemento())* (<COMA> tipo() <ID> (complemento())*)*
        } 

        void complemento():{}{
            <NULO> 
            |<NO_NULO>
            |<AUTOINCREMENTABLE>
            |<LLAVE_PRIMARIA>
            |<LLAVE_FORANEA> <ID>
            |<UNICO>
        }

        void retorno():{}{
            <RETORNO> valor() <PUNTOYCOMA>
        }

    //DDL USAR
        void usar():{}{
            <USAR> <ID> <PUNTOYCOMA>
        }

    //DDL ALTERAR
        void alterar():{}{
            <ALTERAR> alterarCosa()
        }

        void alterarCosa():{}{
            <TABLA> <ID> tipoAlterar() <PUNTOYCOMA>
            |<OBJETO> <ID> tipoAlterar() <PUNTOYCOMA>
            |<USUARIO> <ID> <CAMBIAR> <PASSWORD> <IGUAL> valor() <PUNTOYCOMA>
        }

        void tipoAlterar():{}{
            <AGREGAR> <PAR1> atrTabla() <PAR2>
            |<QUITAR> enviados()
        }
    
    //DDL ELIMINAR
        void eliminar():{}{
            <ELIMINAR> eliminarCosa()
        }

        void eliminarCosa():{}{
            <TABLA> <ID> <PUNTOYCOMA>
            |<BASE_DATOS> <ID> <PUNTOYCOMA>
            |<OBJETO> <ID> <PUNTOYCOMA>
            |<USUARIO> <ID> <PUNTOYCOMA>    
        }

    //DDL BACKUP
        void backup():{}{
            <BACKUP> tipoBackup() <ID> <ID> <PUNTOYCOMA>
        }

        void tipoBackup():{}{
            <USQLDUMP>
            |<COMPLETO>
        }

    //DDL RESTAURAR
        void restaurar():{}{
            <RESTAURAR> tipoBackup() <CADENA> <PUNTOYCOMA>
        }

/** SENTENCIAS DML */
      void dml():{}{ 
        insertar() 
        |actualizar() <PUNTOYCOMA>
        |borrar() <PUNTOYCOMA>
        |seleccionar() <PUNTOYCOMA>
      }
      //DML INSERTAR
            void insertar():{}{ 
                <INSERTAR> <EN> <TABLA> <ID> <PAR1> enviados() <PAR2> (<VALORES> <PAR1> enviados() <PAR2>)? <PUNTOYCOMA>
            }


      //DML ACTUALIZAR
            void actualizar():{}{ 
                <ACTUALIZAR> <TABLA> <ID> <PAR1> enviados() <PAR2> <VALORES> <PAR1> enviados() <PAR2> (restricciones())*
            }

            void restricciones():{}{
                <DONDE> valor()
                |<ORDENAR_POR> <ID> tipoOrden()
            }

      //DML BORRAR
            void borrar():{}{ 
                <BORRAR> <EN> <TABLA> <ID> (restricciones())*
            }

      //DML SELECCIONAR
            void seleccionar():{}{ 
                <SELECCIONAR> seleccionCampos() <DE> enviados() (restricciones())*
            }

            void tipoOrden():{}{ 
                <ASC>
                |<DESC>
            }

            void seleccionCampos():{}{ 
                <ASTERISCO>
                |enviados()
            }

/** SENTENCIAS DCL */
      void dcl():{}{ 
        otorgar()
        |denegar()
      }

      //DCL OTORGAR
            void otorgar():{}{ 
                <OTORGAR> <PERMISOS> <ID> <COMA> <ID> <PUNTO> seleccionCampos() <PUNTOYCOMA>
            }
      //DCL DENEGAR
            void denegar():{}{ 
                <DENEGAR> <PERMISOS> <ID> <COMA> <ID> <PUNTO> seleccionCampos() <PUNTOYCOMA>
            }

/** SENTENCIAS SSL */
      void ssl():{}{ 
        declaracion() <PUNTOYCOMA>
        |asignacion()
        |si()
        |selecciona()
        |para()
        |mientras()
        |detener()
        |imprimir()
        |fecha()
        |fechahora()
        |contar() <PUNTOYCOMA>
      }

      //SSL DECLARACION
            void declaracion():{}{ 
                <DECLARAR> <ID> (<COMA> <ID>)* tipo() (<IGUAL> valor())?
            }

      //SSL ASIGNACION
            void asignacion():{}{ 
                llamada() <IGUAL> valor() <PUNTOYCOMA>
            }

      //SSL SI
            void si():{}{ 
                <SI> <PAR1> valor() <PAR2> <LLA1> sentencias() <LLA2> (<SINO> <LLA1> sentencias() <LLA2>)?
            }

      //SS SELECCIONA
            void selecciona():{}{ 
                <SELECCIONA> <PAR1> valor() <PAR2> <LLA1> (caso())+ <LLA2>
            }

            void caso():{}{ 
                <CASO> valor() <DOSPUNTOS> sentencias()
                |<DEFECTO> <DOSPUNTOS> sentencias()
            }

      //SSL PARA
            void para():{}{ 
                <PARA> <PAR1> <DECLARAR> <ID> <INTEGER> <IGUAL> valor() <PUNTOYCOMA> valor() <PUNTOYCOMA> paso() <PAR2> <LLA1> sentencias() <LLA2>
            }

            void paso():{}{ 
                <MASMAS>
                |<MENOSMENOS>
            }

      //SSL MIENTRAS
            void mientras():{}{ 
                <MIENTRAS> <PAR1> valor() <PAR2> <LLA1> sentencias() <LLA2>
            }

      //SSL DETENER
            void detener():{}{ 
                <DETENER> <PUNTOYCOMA>
            }

      //SSL IMPRIMIR
            void imprimir():{}{ 
                <IMPRIMIR> <PAR1> valor() <PAR2> <PUNTOYCOMA>
            }

      //SSL FECHA Y HORA
            void fecha():{}{ 
                <FECHA> <PAR1> <PAR2> <PUNTOYCOMA>
            }

            void fechahora():{}{ 
                <FECHA_HORA> <PAR1> <PAR2> <PUNTOYCOMA>
            }

      //SSL CONTAR
            void contar():{}{ 
                <CONTAR> <PAR1> <MENOR> <MENOR> seleccionar() <MAYOR> <MAYOR> <PAR2>
            }

            
void tipo():{}{ <TEXT>
      |<INTEGER>
      |<DOUBLE>
      |<BOOL>
      |<DATE>
      |<DATETIME>
      |<ID>
}

//VARIABLES INICIAN CON @

void valor():{}{ 
    valor2() (<OR> valor2())*
}

void valor2():{}{ 
    valor3() (<AND> valor3())*
}


void valor3():{}{ 
    <NOT> valor4()
    |valor4()
}

void valor4():{}{ 
    valor5() (valor4P())*
}

void valor4P():{}{ 
    <MAYOR> valor5() 
      |<MENOR> valor5() 
      |<MAYORIGUAL> valor5() 
      |<MENORIGUAL> valor5() 
      |<IGUALIGUAL> valor5() 
      |<NOIGUAL> valor5() 
}

void valor5():{}{ 
    valor6() (valor5P())*
}

void valor5P():{}{ 
    <MAS> valor6() 
    |<MENOS> valor6() 
}

void valor6():{}{ 
    valor7() (valor6P())*
}

void valor6P():{}{ 
    <POR> valor7()
    |<DIV> valor7() 
}

void valor7():{}{ 
    valor9() (<POT> valor9())*
}

void valor9():{}{   
    <CADENA>
    |<NUMERO>
    |<VERDADERO>
    |<FALSO>
    |<MENOS> valor9()
    |llamada()
    |fecha()
    |fechahora()
    |contar()
    |<PAR1> valor() <PAR2>
}

void llamada():{}{ 
    <ID> (<PUNTO> <ID>)* (<PAR1> (enviados())? <PAR2>)?
}

void enviados():{}{ 
    valor() (<COMA> valor())*
}


